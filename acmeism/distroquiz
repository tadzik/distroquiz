use v6;
use JSON::Tiny;

class Question {
    has $.question;
    has $.yes;
    has $.no;
}

my %data = from-json slurp 'db.json';
my %answers;
my %questions;

for %data<answers>.kv   -> $k, $v {
    %answers{$k} = $v;
}

for %data<questions>.kv -> $k, $v {
    %questions{$k} = Question.new(
        question => $v<question>,
        yes      => $v<yes>[0] eq "answer"
                    ?? sub { %answers{$v<yes>[1]}  }
                    !! sub { %questions{$v<yes>[1]} },
        no       => $v<no>[0] eq "answer"
                    ?? sub { %answers{$v<no>[1]}  }
                    !! sub { %questions{$v<no>[1]} },
    );
}

my $current = %questions{%data<root>};

=begin graph
        (czy miałeś kiedykolwiek do czynienia z linuksem?)
                       tak/         \nie
                (czy chce Ci się     \
                marnować czas na      \
                uczenie się nowego     \
                systemu?)-----nie-------(ubuntu, mint)
             tak/
               /
    (czy bardzo przeszkadza Ci kompilacja?)
          tak/          \
            /            \nie
    (debian, arch)   (gentoo, sabayon)    
=end graph

while $current ~~ Question {
    my $ans = prompt $current.question ~ ' ';
    if $ans ~~ any(/yes/, /tak/) {
        $current = $current.yes.();
    } else {
        $current = $current.no.();
    }
}

say "Dystrubucje dla Ciebie to mniej-więcej: ", $current.join(', ');

# vim: ft=perl6
